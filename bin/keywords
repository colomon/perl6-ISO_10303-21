#!/usr/bin/env perl6

use v6;
use ISO_10303_21::Grammar;

for @*ARGS -> $file {
    my $keyword-bag = BagHash.new;
    my $actions = class { 
        method keyword($/) { $keyword-bag{~$/}++ }
    };

    say "Reading $file";
    my $file-data = slurp($file, encoding => "latin1");
    $file-data .= subst(/"/*" .*? "*/"/, " ", :global);
    
    say "Parsing $file";
    my $match = ISO_10303_21::LooseGrammar.parse($file-data, 
                                                 :rule<exchange_file>, 
                                                 :$actions);
    if $match ~~ Match && $match {
        say "    read correctly!";
    } else {
        say "    failed!!!!";
    }
    
    say "Contents:";
    for $keyword-bag.pairs.sort -> Pair (:$key, :$value) {
        say "$key: $value";
    }
}
